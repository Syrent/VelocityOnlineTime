plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
}

group = 'ir.syrent'
version = findProperty("version")

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }

    // SpigotAPI
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }


    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }

    maven {
        name = "placeholderapi"
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        url = uri("https://maven.pkg.github.com/mohamad82bz/ruom")
        credentials {
            username = project.findProperty("gpr.user")
            password = project.findProperty("gpr.key")
        }
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'me.clip:placeholderapi:2.11.2'

    implementation 'com.github.cryptomorin:XSeries:9.1.0'
    implementation 'me.mohamad82:ruom-velocity:4.3.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'me.mohamad82:ruom-core:4.2.5'
    implementation 'commons-io:commons-io:2.6'
    implementation 'io.papermc:paperlib:1.0.7'

    implementation 'net.kyori:adventure-api:4.11.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.1.2'
    implementation 'net.kyori:adventure-text-minimessage:4.11.0'

    implementation 'mysql:mysql-connector-java:8.0.30'
    implementation 'com.zaxxer:HikariCP:4.0.3'

    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    minimize {
        exclude(dependency('mysql:.*:.*'))
    }

    manifest {
        attributes["Main-Class"] = 'ir.syrent.velocityonlinetime.velocity.VelocityOnlineTime'
    }

    relocate('me.mohamad82.ruom', 'ir.syrent.velocityonlinetime.ruom')
    relocate('com.cryptomorin.xseries', 'ir.syrent.velocityonlinetime.xseries')
    relocate('com.zaxxer.hikari', 'ir.syrent.velocityonlinetime.hikari')
    relocate('net.kyori.adventure', 'ir.syrent.velocityvanish.kyori.adventure')
    relocate('org.bstats', 'ir.syrent.velocityvanish.bstats')

    // @see https://youtrack.jetbrains.com/issue/KT-25709
    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_module'
    exclude '**/*.kotlin_builtins'

    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + ".jar"
}

jar {
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + " " + "unshaded" + ".jar"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

compileJava.options.encoding = 'UTF-8'
compileJava.dependsOn clean

build.dependsOn shadowJar

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/bin/")
}

clean.doLast {
    file("${rootDir}/bin").deleteDir()
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
